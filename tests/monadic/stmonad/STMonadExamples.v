From Crane Require Import Monads.STMonad Monads.ITree.   

From Stdlib Require Import
  Arith.PeanoNat
  Arith.Peano_dec
  Init.Peano
  Lia
  List
  Morphisms
  RelationClasses
  Relation_Definitions
  Setoid
  Strings.String
  Classes.EquivDec
  Basics
.

From ExtLib Require Import
  CmpDec
  Data.Bool
  Data.List
  Data.Map.FMapAList
  Data.Monads.EitherMonad
  Data.Pair
  Data.String
  Structures.Functor
  Structures.Maps
  Structures.Traversable
  Structures.Reducible
.


From ITree Require Import
  Events.Exception
  Events.FailFacts
  Events.MapDefault
  Events.MapDefaultFacts
  Events.State
  Events.StateFacts
  ITree
  ITreeFacts
.


From Equations Require Import Equations.


Import Monads.
Import ListNotations.
Import ProperNotations.
Local Open Scope monad_scope.
Local Open Scope string_scope.


Section NatExampleTrees.

  Context {T S : Type}.
  Context {ltu : T -> T -> Prop}.
  Context `{Ix_Correct T ltu}.
  Context {HST: STRefClass T}.

  Let V : T -> Type := fun _ => nat. (* Nats only for this example. *)
  Let E0 := (STEvent T S V) +' exceptE Err.


  (* TODO: autogenerate successive indices here? *)
  Definition new_and_read_both_nat : itree E0 (nat * nat) :=
      r1 <- newSTRef zero 5 ;;
      r2 <- newSTRef (suc zero) 6 ;; 
      x1 <- readSTRef r1 ;;
      x2 <- readSTRef r2 ;;
      Ret (x1, x2).

  Definition tree_simp_nat : itree E0 nat :=
    v <- newSTRef zero 5;;
    readSTRef v.

  (* NOTE: this failing definition is intentional.
    The intent is to test that we don't allow reference indices to escape. *)
  Fail Definition tree_escape_nat : itree E0 nat :=
    v <- newSTRef 5;;
    writeSTRef v (match v with mkSTRef _ _ idx => idx end);;
    readSTRef v.

  Definition tree_simp_another_nat : itree E0 nat :=
    v <- newSTRef zero 5;;
    writeSTRef v 6;;
    val <- readSTRef v;;
    Ret val.


   Definition swap' (v w : STRef S nat) : itree E0 unit :=
    a <- @readSTRef E0 T S  _ V  _ (STRefToIx _ _ v) v;;
    b <- @readSTRef E0 T S _ V _ (STRefToIx _ _ w) w;;
    writeSTRef v b;;
    writeSTRef w a.

  (* "swap" function from "Lazy Functional State Threads", by John Launchbury and Simon L Peyton Jones. *)
  (* TODO: would be good for indices here (and everywhere in the file) to be inferrable. *)
  Fail Definition swap (v w : STRef S nat) : itree E0 unit :=
    a <- readSTRef v;;
    b <- readSTRef w;;
    writeSTRef v b;;
    writeSTRef w a.


  Definition array_simp_fixed_init : itree E0 nat :=
    arr <- newArray zero zero (suc (suc (suc (suc (suc zero))))) 5;;
    elem <- @readArray E0 T _ _ _ _ _ arr (suc (zero));;
    Ret elem. 
  
  Definition array_simp_list : itree E0 (nat * nat * list nat) :=
    arr <- newListArray zero zero (suc (suc (suc zero))) [5;4;3;2];;
    elem <- @readArray _ _ _ _ _ _ zero arr zero;;
    lst <- @getElems _ _ _ _ _ _ zero arr;;
    Ret (elem, length lst, lst). 



  (* source: https://wiki.haskell.org/Monad/ST *)

  Fixpoint fib_loop (k : nat) (x y : STRef S nat) (idx_x idx_y : T) : itree E0 nat :=
    match k with
    | 0 => @readSTRef _ _ _ _ _ _ idx_x x
    | Datatypes.S k' =>
        x' <- @readSTRef _ _ _ _ _ _ idx_x x;;
        y' <- @readSTRef _ _ _ _ _ _ idx_y y;;
        @writeSTRef _ _ _ _ _ _ idx_x x y';;
        @writeSTRef _ _ _ _ _ _ idx_y y (x' + y');;
        fib_loop k' x y idx_x idx_y
    end.

  Definition fib_ST (n : nat) : itree E0 nat :=
    if (Nat.ltb n 2)
    then Ret n
    else
      x <- newSTRef zero 0;;
      y <- newSTRef (suc zero) 1;;
      fib_loop n x y zero (suc zero).

  Definition fib_fun (n : nat) : nat :=
    let fix fib' (n : nat) :=
      match n with
      | 0 => 0
      | 1 => 1
      | Datatypes.S (Datatypes.S m as m0) => fib' m0 + fib' m
      end in
    fib' n.

  Section QSort. 


    Definition swap_arr
       {E' : Type -> Type}
      `{STEvent T S V -< E'}
      `{exceptE Err -< E'}
      (arr : STArray T S nat) (arr_idx : T) (left : T) (right : T) : itree E' unit :=
      leftVal <- readArray arr left;;
      rightVal <- readArray arr right;;
      @writeArray _ T S _ _ _ arr_idx arr left rightVal;;
      @writeArray _ T S _ _ _ arr_idx arr right leftVal.

  
    Definition swap_first_and_last_list
       {E' : Type -> Type}
      `{STEvent T S V -< E'}
      `{exceptE Err -< E'}
      (xs : list nat) : itree E' (list nat) :=
      let lastIndex := fromNat (length xs - 1) in 
      arr <- newListArray zero zero lastIndex xs;;
      swap_arr arr zero zero lastIndex;;
      newXs <- getElems arr;;
      Ret newXs.

    (* NOTE: would be nice to use following definition, but it does not extract well
    foldM (flip f) (Ret v) (rev xs). (* reversing so foldM goes left to right. *) *)
    Fixpoint for_each_with {A B}
       {E' : Type -> Type}
      (xs : list A) (v : B) (f : B -> A -> itree E' B)
      : itree E' B :=
      match xs with
      | nil => Ret v
      | h::t => v' <- f v h;; for_each_with t v' f
      end.



    Definition partition 
       {E' : Type -> Type}
      `{STEvent T S V -< E'}
      `{exceptE Err -< E'}
      (arr : STArray T S nat) (arr_idx : T) (left : T) (right : T) (pivotIndex : T) : itree E' T :=
      pivotValue <- @readArray _ _ _ _ _ _ arr_idx arr pivotIndex;;
      swap_arr arr arr_idx pivotIndex right;;
      storeIndex <- for_each_with (range left (sub right (suc zero))) left (fun storeIndex i =>
          val <- @readArray _ _ _ _ _ _ arr_idx arr i;;
          if (Nat.leb val pivotValue)
              then swap_arr arr arr_idx i storeIndex;;
                  Ret (suc storeIndex)
              else Ret storeIndex );;
      swap_arr arr arr_idx storeIndex right;;
      Ret storeIndex.


    (* For quicksort, we found it easiest to use the `rec` (internally, a call
       to `mrec`) function from the ITree library, which uses "call" events to
       represent recursive calls, and then interprets them in a context with the body (ala fixpoint combinators)
       to produce a tree representing the recursive behavior.
       It might seem possible to define this recursive function directly using CoFixpoint,
       replacing the `call` below with Tau wrapping the recursive call, but this is not possible because
       we have to sequence *two* recursive calls here, and it is not possible to
       to define this admissably for Rocq's syntactic guard condition. If we define quicksort with
       two recursive calls, we cannot admissably place `Tau (qsort ...<args>...)` on the left side of a bind.
     *)
    Definition quicksort_ST_body 
      (args : (STArray T S nat * T * T * T))
      : itree ((callE (STArray T S nat * T * T * T) unit) +' E0) unit :=
      let '(arr,arr_idx,l,r) := args in
      if (Nat.ltb (toNat l) (toNat r)) then
        let leftn := toNat l in
        let rightn := toNat r in 
        let pivotIndexn := leftn + ((rightn - leftn) / 2) in
        newPivot <- partition arr arr_idx l r (fromNat pivotIndexn);;
        call (arr, arr_idx, l, (fromNat ((toNat newPivot) - 1)));;
        call (arr, arr_idx, (fromNat ((toNat newPivot) + 1)), r)
      else Ret tt.


    Definition quicksort_ST (arr : STArray T S nat) (arr_idx : T) (left : T) (right : T) : itree E0 unit :=
      rec quicksort_ST_body (arr, arr_idx, left, right).

    
    Definition quicksort_ST_list (xs : list nat) : itree E0 (list nat) :=
      match xs with
      | [] => Ret []
      | _::_ => 
        let lastIndex := fromNat (length xs - 1) in 
        arr <- newListArray zero zero lastIndex xs;;
        quicksort_ST arr zero zero lastIndex;;
        newXs <- getElems arr;;
        Ret newXs
      end.
  


  End QSort.

  
End NatExampleTrees.



  Lemma filter_length {A} (f : A -> bool) (l : list A) :
    length (List.filter f l) <= length l.
  Proof. induction l; simpl; [lia | destruct (f a); simpl; lia]. Qed.

  Section FunctionalQuicksort.

    Equations? quicksort_fun (l : list nat) : list nat by wf (length l) lt :=
      quicksort_fun [] => [];
      quicksort_fun (p :: xs) =>
        quicksort_fun (List.filter (fun x => Nat.ltb x p) xs)
          ++ [p] ++
        quicksort_fun (List.filter (fun x => Nat.leb p x) xs).
    - specialize (filter_length (fun x => Nat.ltb x p) xs) as H. lia.   
    - specialize (filter_length (fun x => Nat.leb p x) xs) as H. lia.   
    Defined.

End FunctionalQuicksort.


Section BoolExampleTrees.

  Context {E : Type -> Type}.
  Context {T S : Type}.
  Context {ltu : T -> T -> Prop}.
  Context `{Ix_Correct T ltu}.
  Context {HST: STRefClass T}.

  Let V : T -> Type := fun _ => bool. (* bools only for this example. *)
  Let E0 := (STEvent T S V) +' exceptE Err.


  Definition new_and_read_both_bool : itree E0 (bool * bool) :=
      r1 <- newSTRef zero false ;;
      r2 <- newSTRef (suc zero) true ;; 
      x1 <- readSTRef r1 ;;
      x2 <- readSTRef r2 ;;
      Ret (x1, x2).

  Definition tree_simp_bool : itree E0 bool :=
    v <- newSTRef zero true;;
    readSTRef v.

End BoolExampleTrees.
